plugins {
    id 'java-gradle-plugin'
    id 'com.jrmcdonald.ms-gradle-baseline' version "${gradleBaselinePluginVerson}"
    id 'com.gradle.plugin-publish' version "${gradlePluginPublishPluginVersion}"
}

group = 'com.jrmcdonald'
version = "${version}"

ext['gradle.publish.key'] = System.getenv("GRADLE_PUBLISH_KEY")
ext['gradle.publish.secret'] = System.getenv("GRADLE_PUBLISH_SECRET")

repositories {
    mavenCentral()
    gradlePluginPortal()
}

apply from: "${rootDir}/gradle/publishing.gradle"
apply from: "${rootDir}/gradle/functional-test.gradle"

configurations {
    commonPlatform
    compileOnly.extendsFrom(commonPlatform)
    annotationProcessor.extendsFrom(commonPlatform)
    testCompileOnly.extendsFrom(commonPlatform)
    testAnnotationProcessor.extendsFrom(commonPlatform)
    functionalTestCompileOnly.extendsFrom(commonPlatform)
    functionalTestAnnotationProcessor.extendsFrom(commonPlatform)
    implementation.extendsFrom(commonPlatform)
    testFixturesImplementation.extendsFrom(commonPlatform)
}

dependencies {
    commonPlatform enforcedPlatform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: "${springbootPluginVersion}")

    compileOnly group: 'org.projectlombok', name: 'lombok'
    annotationProcessor group: 'org.projectlombok', name: 'lombok'

    // Plugin Dependencies
    implementation group: 'gradle.plugin.com.github.spotbugs.snom', name: 'spotbugs-gradle-plugin', version: "${spotbugsPluginVersion}"
    implementation group: 'gradle.plugin.com.gtramontina.ghooks.gradle', name: 'ghooks.gradle', version: "${ghooksPluginVersion}"
    implementation group: 'com.github.ben-manes', name: 'gradle-versions-plugin', version: "${versionsPluginVersion}"
    implementation group: 'org.owasp', name: 'dependency-check-gradle', version: "${dependencyCheckPluginVersion}"
    implementation group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: "${sonarqubePluginVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${springbootPluginVersion}"

    testCompileOnly group: 'org.projectlombok', name: 'lombok'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

    testImplementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${spotBugsAnnotationsVersion}"
    testImplementation group: 'org.assertj', name: 'assertj-core'
    testImplementation group: 'org.force66', name: 'BeanTester', version: "${beanTesterVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'
    testImplementation group: 'org.mockito', name: 'mockito-core'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter'

    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok'
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

    functionalTestImplementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: "${jgitVersion}"
}

jar {
    enabled = true
}

tasks.withType(Test) {
    useJUnitPlatform()
}
